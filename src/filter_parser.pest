WHITESPACE = _{ " " }
CMD_START = _{ ":" }
CMD_END = _{("="|"/")}
CMD_SEP = _{("&"|NEWLINE)}


filter_spec = { ( filter_compose | filter_subdir | filter | filter_noarg )+ }

filter_compose = { CMD_START ~ "(" ~ compose ~ ")" }
filter_subdir = { CMD_START ~ "/" ~ argument }
filter = { CMD_START ~ cmd ~ "=" ~ (argument ~ ("," ~ argument)*)? }
filter_noarg = { CMD_START ~ cmd }

argument = { (!(CMD_START | CMD_SEP | "," | "(" | ")") ~ ANY)+ }

cmd = { (!(CMD_END | CMD_SEP | CMD_START | "(" | ")") ~ ANY)+ }

workspace_file = {
    NEWLINE*
    ~ (filter_spec|file_entry)? 
    ~ (CMD_SEP+ ~ (filter_spec|file_entry))*
    ~ NEWLINE*
    ~ EOI
}

compose = {
    NEWLINE*
    ~ (filter_spec|file_entry)
    ~ (CMD_SEP+ ~ (filter_spec|file_entry))*
    ~ NEWLINE*
}

file_entry = { dst_path ~ ("=" ~ filter_spec)? }

dst_path = @{ path ~ ("/" ~ path)* }
path = @{ (!("=" | "/" | CMD_START | WHITESPACE | CMD_SEP | "(" | ")") ~ ANY)+ }
