WHITESPACE = _{ " " }
CMD_START = _{ ":" }
CMD_END = _{("="|"/")}
CMD_SEP = _{(","|NEWLINE)}
ALNUM = _{( ASCII_ALPHANUMERIC | "_" | "-" | "+" | "." | "*" )}
GROUP_START = _{ "[" }
GROUP_END = _{ "]" }

filter_spec = { (
    filter_group
  | filter_presub
  | filter_subdir
  | filter_nop
  | filter
  | filter_noarg
)+ }

filter_group = { CMD_START ~ cmd? ~ GROUP_START ~ compose ~ GROUP_END }
filter_subdir = { CMD_START ~ "/" ~ argument }
filter_nop = { CMD_START ~ "/" }
filter_presub = { CMD_START ~ ":" ~ argument }
filter = { CMD_START ~ cmd ~ "=" ~ (argument ~ ("," ~ argument)*)? }
filter_noarg = { CMD_START ~ cmd }

argument = { (ALNUM | "/" )+ }

cmd = { ALNUM+ }

workspace_file = {
    NEWLINE*
    ~ (filter_spec|file_entry)? 
    ~ (CMD_SEP+ ~ (filter_spec|file_entry))*
    ~ NEWLINE*
    ~ EOI
}

filter_chain = _{ filter_spec ~ EOI }

compose = {
    NEWLINE*
    ~ (filter_spec|file_entry)
    ~ (CMD_SEP+ ~ (filter_spec|file_entry))*
    ~ NEWLINE*
}

file_entry = { dst_path ~ "=" ~ filter_spec }

dst_path = @{ path ~ ("/" ~ path)* }
path = @{ ALNUM+ }
